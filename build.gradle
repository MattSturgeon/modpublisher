plugins {
    id 'java'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id "com.diffplug.spotless" version "6.13.0"
}
apply plugin: 'maven-publish'

group = 'me.hypherionmc.modutils'
version = "${version_base}.${version_patch}"
description = "Gradle plugin to Upload and Publish mods to Curseforge/Modrinth/Github"
archivesBaseName = 'ModPublisher'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

if (System.getenv('BUILD_NUMBER') != null) {
    version = "${version_base}." + System.getenv('BUILD_NUMBER')
}

configurations {
    shadeMe
    implementation.extendsFrom shadeMe
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.firstdarkdev.xyz/releases"
    }
}

dependencies {
    implementation gradleApi()

    shadeMe "me.hypherionmc.modutils:CurseUpload4j:1.0.9"
    shadeMe "dev.masecla:Modrinth4J:2.1.0"
    shadeMe "org.kohsuke:github-api:1.314"
    shadeMe "org.apache.maven:maven-artifact:4.0.0-alpha-5"

    shadeMe 'org.ow2.asm:asm:9.5'
    shadeMe 'org.ow2.asm:asm-tree:9.5'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.getByName("shadeMe")]
    archiveClassifier.set(null)
}

gradlePlugin {
    plugins {
        modPublisherPlugin {
            id = 'me.hypherionmc.modutils.modpublisher'
            description = project.description
            displayName = 'ModPublisher'
            version = project.version
            implementationClass = "me.hypherionmc.modpublisher.plugin.ModPublisherPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/firstdarkdev/modpublisher'
    vcsUrl = 'https://github.com/firstdarkdev/modpublisher'
    description = 'Gradle plugin to Upload and Publish mods to Curseforge/Github/Modrinth in one go'
    tags = ['modrinth', 'publish', 'mods', 'github', 'minecraft', 'curseforge']
}

jar {
    manifest {
        attributes([
                'Timestamp'               : System.currentTimeMillis(),
                'Specification-Title'     : project.archivesBaseName,
                'Specification-Version'   : project.version,
                'Implementation-Title'    : project.archivesBaseName,
                'Implementation-Version'  : project.version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}

spotless {
    java {
        targetExclude("src/test/**", "src/main/java/me/hypherionmc/modpublisher/util/scanner/**")
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
    }
}

publishing {
    repositories {
        maven {
            url System.getenv('MAVEN_URL')
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}
