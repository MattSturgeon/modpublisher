plugins {
    id 'java'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'com.diffplug.spotless' version '6.13.0'
}
apply plugin: 'maven-publish'

group = 'com.hypherionmc.modutils'
version = "${version_base}.${version_patch}"
description = "Gradle plugin to Upload and Publish mods to Curseforge/Modrinth/Github"
archivesBaseName = 'ModPublisher'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

if (System.getenv('BUILD_NUMBER') != null) {
    var build = (System.getenv('BUILD_NUMBER').toInteger() - 1)
    version = "${version_base}.${build}"
}

configurations {
    shadeMe
    implementation.extendsFrom shadeMe
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.firstdark.dev/releases"
    }
}

dependencies {
    implementation gradleApi()

    shadeMe "me.hypherionmc.modutils:CurseUpload4j:${curse4j}"
    shadeMe "dev.masecla:Modrinth4J:${modrinth4j}"
    shadeMe "org.kohsuke:github-api:${github}"
    shadeMe "org.apache.maven:maven-artifact:${mavenart}"

    shadeMe "org.ow2.asm:asm:${asm}"
    shadeMe "org.ow2.asm:asm-tree:${asm}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit}"
}

test {
    useJUnitPlatform()
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.getByName("shadeMe")]
    archiveClassifier.set(null)
}

gradlePlugin {
    plugins {
        modPublisherPlugin {
            id = 'com.hypherionmc.modutils.modpublisher'
            description = project.description
            displayName = 'ModPublisher'
            version = project.version
            implementationClass = "com.hypherionmc.modpublisher.plugin.ModPublisherPlugin"
            tags.set(['modrinth', 'publish', 'mods', 'github', 'minecraft', 'curseforge'])
        }
    }
}

jar {
    manifest {
        attributes([
                'Timestamp'               : System.currentTimeMillis(),
                'Specification-Title'     : project.archivesBaseName,
                'Specification-Version'   : project.version,
                'Implementation-Title'    : project.archivesBaseName,
                'Implementation-Version'  : project.version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}

spotless {
    java {
        targetExclude("src/test/**", "src/main/java/me/hypherionmc/modpublisher/util/scanner/**")
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
    }
}

publishing {
    repositories {
        maven {
            url System.getenv('MAVEN_URL')
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}
